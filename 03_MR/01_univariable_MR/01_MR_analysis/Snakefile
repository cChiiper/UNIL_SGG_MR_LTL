EXPO_TRAITS = ["TELOMERE"]
OUTCOME_TRAITS = ["BODYSIZE10"]

### Setting directory paths
path_to_ma_files = "/SET/PATH/TO/DIRECTORY/"
path_to_output = "/SET/PATH/TO/DIRECTORY/"

rule all:
    input:
        #expand(path_to_output + "{expo_trait}_snps.txt", expo_trait = EXPO_TRAITS),
        #expand(path_to_output + "{expo_trait}_to_{outcome_trait}_MR_data.tsv", expo_trait = EXPO_TRAITS, outcome_trait = OUTCOME_TRAITS),
        expand(path_to_output + "{expo_trait}_to_{outcome_trait}_twoSampleMR_result.tsv", expo_trait = EXPO_TRAITS, outcome_trait = OUTCOME_TRAITS),

rule select_gwas_snps:
    input:
        path_to_ma_files + "{expo_trait}_gwas_summary_uk10kck.ma"
    output:
        path_to_output + "{expo_trait}_snps.txt"
    shell:
        "awk '{{if ($7 < 5e-8) {{ print $1 }} }}' {input} > {output}"

rule harmonize_snps:
    input:
        path_to_output + "{expo_trait}_snps.txt",
        path_to_ma_files + "{outcome_trait}_gwas_summary_uk10kck.ma"
    output:
        path_to_output + "{expo_trait}_to_{outcome_trait}_snps.txt"
    script:
        "scripts/harmonize_snps.py"

rule clump_harmonized_snps:
    input:
        gwas = path_to_ma_files + "{expo_trait}_gwas_summary_uk10kck.ma",
        expo_snps = path_to_output + "{expo_trait}_to_{outcome_trait}_snps.txt",
        bfile = "uk10k.autosomal.bed"
    params:
        bfile = "uk10k.autosomal",
        out = path_to_output + "{expo_trait}_to_{outcome_trait}_clump"
    output:
        clump_input = temp("{expo_trait}_to_{outcome_trait}_snp_pval.txt"),
        clump_output = temp(path_to_output + "{expo_trait}_to_{outcome_trait}_clump.clumped"),
        clump_log = temp(path_to_output + "{expo_trait}_to_{outcome_trait}_clump.log"),
        clump_nosex = temp(path_to_output + "{expo_trait}_to_{outcome_trait}_clump.nosex"),
        clumped_expo_snps = path_to_output + "{expo_trait}_to_{outcome_trait}_clumped_snps.txt"
    shell:
        """
        awk {{'print $1"\t"$7'}} {input.gwas} > {output.clump_input}

        plink --bfile {params.bfile} --extract {input.expo_snps} --clump {output.clump_input} --clump-r2 0.01 --clump-field 'p' --out {params.out}

        awk '$3 ~ /^rs/ {{print $3 }}' {output.clump_output} > {output.clumped_expo_snps}
        """

rule prepare_MR_data:
    input:
        expo_df = path_to_ma_files + "{expo_trait}_gwas_summary_uk10kck.ma",
        outcome_df = path_to_ma_files + "{outcome_trait}_gwas_summary_uk10kck.ma",
        snps = path_to_output + "{expo_trait}_to_{outcome_trait}_clumped_snps.txt"
    output:
        path_to_output + "{expo_trait}_to_{outcome_trait}_MR_data.tsv"
    script:
        "scripts/prepare_MR_data.R"

rule twoSampleMR:
    input:
        path_to_output + "{expo_trait}_to_{outcome_trait}_MR_data.tsv"
    output:
        path_to_output + "{expo_trait}_to_{outcome_trait}_twoSampleMR_result.tsv"
    script:
        "scripts/twoSampleMR.R"
